(call: medical-start )
(call: ferpaCompliance )
(call: cipseaPaCompliance )
(todo: Arrest and Conviction Records, Bank and Financial Records, 
	Cable Television, Computer Crime, 
	Credit reporting and Investigations [including ‘Credit Repair,’ ‘Credit Clinics,’ Check-Cashing and Credit Cards],
	Criminal Justice Information Systems, Electronic Surveillance [including Wiretapping, Telephone Monitoring, and Video Cameras],
	Employment Records, Government Information on Individuals, Identity Theft,
	Insurance Records [including use of Genetic Information], Library Records, 
	Mailing Lists [including Video rentals and Spam],
	Special Medical Records [including HIV Testing],
	Non-Electronic Visual Surveillance. Breast-Feeding, Polygraphing in Employment,
	Privacy Statutes/State Constitutions [including the Right to Publicity],
	Privileged Communications, Social Security Numbers, Student Records, Tax Records,
	Telephone Services [including Telephone Solicitation and Caller ID], 
	Testing in Employment [including Urinalysis, Genetic and Blood Tests], Tracking Technologies,  Voter Records)
(call: dua)
(end)
(>dua< todo: Data use agreements)
(end)
(>medical-start< ask:
	(text: Person-specific. Does your data include personal information?)
	(terms:
		(data: 0s and 1s in some structured way)
		(personal information: as defined in HIPAA))
	(no:
		(set: code=green, storage=clear, transit=clear, auth=none,
			basis=notApplicable, identity=notPersonSpecific,
			harm=negligible)
	 	(end)
))
(>ec< ask:
(text: Explicit Consent. Did each person whose information appears in the
	data give explicit permission to share the data?)
  (yes:
  	(set: basis=consent)
  	(ask:
  		(text: Did the consent have any restrictions on data sharing?)
  		(no:  (set: code=green, storage=clear, transit=clear, auth=none))
  		(yes: (call: dua)))
  	(end)
))
(>medicalRecords< ask:
	(text:Medical Records. Does the data contain personal health information?)
	(no:
		(set: basis=agreement)
		(ask: (text:Did the data have any restrictions on sharing, such as stated in an agreement or policy statement?)
		      (no:  (set: code=green, storage=clear, transit=clear, auth=none) )
		      (yes: (call: dua))
		))
	(yes:
		(set:  standards=HIPAA )
		(call: hipaaCompliance )
))
(>timeLimit< ask:
	(text:For how long should we keep the data?)
	(forever:  (set: timeLimit=none) )
	(50 years: (set: timeLimit=_50yr) )
	(5 years:  (set: timeLimit=_5yr) )
	(1 year:   (set: timeLimit=_1yr) )
)
(end)

(>hipaaCompliance< ask:
	(text: HIPAA. Was the data received from a HIPAA covered entity or a business associate of one?)
	(yes:
		(ask:
			(text: Safe Harbor. Does the data visually adhere to the HIPAA Safe Harbor Provision?)
			(terms:
				(Safe Harbor Provision: Dates in years, zip codes contain only first 2 digits))
			(yes: (ask: (text:Do you know of a way to to put names to the paitients in the data?)
					  	    (no:  (set: code=green, storage=clear, transit=clear, auth=none, basis=HIPAASafeHarbor, effort=deidentified, harm=negligible)
					  	    	    (end)))))
	)
)
(ask:
	(text: Statistician Provision. Has an expert certified the data as being of minimal risk?)
	(yes: (set:code=green, transit=clear, storage=clear, auth=none, basis=HIPAAStatistician, effort=deidentified, harm=negligible)
		    (end)))
(>3.1.3< ask:
	(text: Limited Data Set. Did you acquire the data under a HIPAA limited data use agreement?)
	(yes:
		(set: storage=encrypt, transit=encrypt, auth=approval, basis=HIPAALimitedDataset, effort=identifiable, harm=criminal)
		(ask:
			(text: Did the limited data use agreement have any additional restrictions on sharing?)
			(yes:(call: dua)))
		(end)
	))
(>3.1.4< ask:
	(text: Business Associate. Did you acquire the data under a HIPAA Business Associate Agreement?)
	(yes:
		(set: code=red, storage=encrypt, transit=encrypt, auth=approval, basis=HIPAABusinessAssociate, effort=identifiable, harm=criminal)
		(ask: (text: Did the business associate agreement have any additional restrictions?)
				  (yes: (call: dua)))
		(end)
	))
(>3.1.5< ask:
	(text: Covered. Are you an entity that is directly or indirectly covered by HIPAA?)
	(yes:
		(set: code=red, storage=encrypt, transit=encrypt, auth=approval, basis=HIPAABusinessAssociate, effort=identifiable, harm=criminal)))
(end)

(>ferpaCompliance< ask:
	(text:Does the data being deposited directly relate to a student, and is it maintained by an educational agency or institution?)
	(terms:
		(educational agency or institution: school, school district, state educational agency)
	)
	(no: (end))
)
(>fcq2< ask:
	(text: 2.	Does the data fall into any of the following categories?
		* Personal memory aids held in the sole possession of their creator;
		* Records maintained by the law enforcement division of an educational agency or institution;
		* Employment records for an educational agency or institution;
		* Records produced by a physician, psychiatrist, or other professional for treatment purposes; or
		* Grades on peer-graded papers before a teacher has recorded them.
	)
	(yes: (end))
)
(set: standards=FERPA)
( >fcq3< ask:
	(text: Does the data include information that, alone or in combination, is linked or linkable to a specific student that would 
							allow a reasonable person to identify the student with reasonable certainty?)
  (terms:
			(reasonable certainty: Examples include: a student’s or student’s family name;
							   student or student’s family’s address;
							   a personal identifier like a social security number or student number;
							   other indirect identifiers like date of birth or mother’s maiden name.))
	(no:
		(set: storage=clear, code=green, harm=negligible, auth=none, effort=deidentified, transit=clear, FERPABasis=FERPADeidentified)
		(end))
)
( >fcq4< ask:
	(text: Did the educational agency or institution classify the education records in question as directory information?)
	(yes:
		( >fcq4a< ask: (text: Did the educational agency or institution give parents and students notice of the type of information they are designating as directory information and the opportunity to opt out of the disclosure of their directory information? )
		  (yes:
				( >fcq4ai< ask:
					(text: Did any of the students in the data request to opt out of the disclosure of their directory information? )
					(yes: (set: storage=encrypt, code=yellow, harm=shame, effort=identifiable, transit=encrypt, FERPABasis=FERPADirectoryOptOut))
					(no:  (set: storage=clear, code=green, harm=negligible, effort=deidentified, transit=clear, FERPABasis=FERPADirectoryInfo)))
				(end))
)))
(set: storage=encrypt, code=yellow, harm=shame, effort=identifiable, transit=encrypt)
( >fqc5< ask:
	(text: Did the parents or students [if over 18] sign a consent form authorizing this disclosure of their data?)
	(yes:
		( >fcq5a< ask: 
			(text: Did the consent form specify the records to be disclosed, the purpose of the disclosure, and to whom the records may be disclosed?)
			(yes: (set: FERPABasis=FERPAConsent)
						(call: FERPA-Consent)
						(end))
		))
)
( >fcq6< ask: 
	(text: Are the data being disclosed to a school official [or contractor, consultant, volunteer, or other party to whom an agency or institution has outsourced institutional services or functions] with legitimate educational interests?)
	(yes: (set: FERPABasis=FERPAOfficial)
			  (call: FERPA-DUA-Official)
			  (end))
)
( >fcq7< ask:
	(text: Are the data being disclosed for the purpose of conducting a study for or on behalf of the educational agency or institution in order to develop, validate, or administer predictive tests; administer student aid projects; or improve instruction?)
	(yes: (set: FERPABasis=FERPAStudies)
				(call: FERPA-DUA-Studies)
				(end))
	(no: 
		(reject: educational agency or institution is likely breaching its FERPA duties because it is disclosing, or a third party is re-disclosing, non-directory PII without parental consent where no obvious FERPA exception applies))
)
(end)

(>FERPA-DUA-Studies< todo )(end)

(>FERPA-DUA-Official< todo )(end)

(>FERPA-Consent< todo )(end)

( >cipseaPaCompliance< ask: 
	(text: Were the data collected by a federal agency?)
	(no: (end))
)
( >csc2< ask:
	(text: Are you an employee or an agent, such as a contractor, consultant, researcher, or employee of a private organization, operating under a contract, grant, or other agreement with the agency that collected the data?)
	(no: (end))
)
( >csc3< ask: 
	(text: Were the data collected directly from respondents for exclusively statistical purposes under a pledge of confidentiality?)
	(yes:
		( >csc4< ask:
			(text: Is the agency that collected the data a statistical agency or unit from this list: USDA Economic Research Service, National Agricultural Statistics Service, Bureau of Economic Analysis, Census Bureau, National Center for Education Statistics, Energy Information Administration, National Center for Health Statistics, Bureau of Justice Statistics, Bureau of Labor Statistics, Bureau of Transportation Statistics, Statistics of Income Division of the Internal Revenue Service, or NSF Division of Science Resources Statistics?)
			(yes:
				(set: standards=CIPSEA)
				(>csc5< ask:
					(text: Are the data being shared in identifiable form?)
					(terms:
						(identifiable form: any representation of information that permits the identity of the respondent to whom the information applies to be reasonably inferred by either direct or indirect means))
					(no: (set: storage=clear, code=green, harm=negligible, auth=none, effort=deidentified, transit=clear, CIPSEABasis=CIPSEADeidentified))
					(yes: (set: storage=encrypt, code=yellow, harm=shame, auth=approval, effort=identifiable, transit=encrypt, CIPSEABasis=CIPSEAIdentifiable)
								(call: DUA-CIPSEA))
				)
			)
		)
	)
)
( >csc6< ask: 
	(text: Are the data maintained by the agency in a system of records?)
	(no: (end))
)
(set: standards=PrivacyAct)
( >csc7< ask: 
	(text: Do the data contain personally identifiable information? Personally identifiable information includes any information about an individual that can be used to distinguish or trace an individual's identity, either by itself or in combination with any other information that is linked or linkable to an individual)
	(terms:
		(individual's identity: Items such as name, address, or a social security number.)
		(information that is linked: Items such as medical, educational, financial, criminal or employment history))
	(no: (set: storage=clear, code=green, harm=negligible, auth=none, effort=deidentified, transit=clear, CIPSEABasis=PrivacyActDeidentified) )
	(yes: (set: storage=encrypt, code=yellow, harm=shame, auth=approval, effort=identifiable, transit=encrypt, CIPSEABasis=PrivacyActIdentifiable)
			 (call: DUA-PrivacyAct))
)
(end)
(>DUA-PrivacyAct< todo)(end)
(>DUA-CIPSEA< todo)