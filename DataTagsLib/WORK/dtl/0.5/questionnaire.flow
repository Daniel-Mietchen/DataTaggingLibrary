(ask:
	(text: Do the data concern living persons?)
	(yes: (set: Code=green) (call:personalData) )
	(no:  (set: Code=blue, Identity=noPersonData) )
)
(call: dua)
(end)

(>personalData< call: medical-start )
(call: ppraCompliance )
(call: ferpaCompliance )
(call: govRecsCompliance )
(todo: Arrest and Conviction Records, Bank and Financial Records, 
	Cable Television, Computer Crime, 
	Credit reporting and Investigations [including 'Credit Repair', 'Credit Clinics', Check-Cashing and Credit Cards],
	Criminal Justice Information Systems, Electronic Surveillance [including Wiretapping, Telephone Monitoring, and Video Cameras],
	Employment Records, Government Information on Individuals, Identity Theft,
	Insurance Records [including use of Genetic Information], Library Records, 
	Mailing Lists [including Video rentals and Spam],
	Special Medical Records [including HIV Testing],
	Non-Electronic Visual Surveillance. Breast-Feeding, Polygraphing in Employment,
	Privacy Statutes/State Constitutions [including the Right to Publicity],
	Privileged Communications, Social Security Numbers, Student Records, Tax Records,
	Telephone Services [including Telephone Solicitation and Caller ID], 
	Testing in Employment [including Urinalysis, Genetic and Blood Tests], Tracking Technologies,  Voter Records)
(end)

(>medical-start< ask:
	(text: Person-specific. Does your data include personal information?)
	(terms:
		(data: 0s and 1s in some structured way)
		(personal information: as defined in HIPAA))
	(no:
		(set: Code=green, Storage=clear, Transit=clear, auth=none,
			Identity=notPersonSpecific,
			Harm=minimal)
    (end)
)) 
(>ec< ask:
(text: Explicit Consent. Did each person whose information appears in the
	data give explicit permission to share the data?)
  (yes:
  	(set: HIPAABasis=consent)
  	(ask:
  		(text: Did the consent have any restrictions on data sharing?)
  		(no:  (set: Code=green, Storage=clear, Transit=clear, auth=none))
  		(yes: (call: hippa-dua)))
  	(end)
))
(>medicalRecords< ask:
	(text:Medical Records. do the data contain personal health information?)
	(no:
		(set: HIPAABasis=agreement)
		(ask: (text:Did the data have any restrictions on sharing, such as stated in an agreement or policy statement?)
		      (no:  (set: Code=green, Storage=clear, Transit=clear, auth=none) )
		      (yes: (call: hippa-dua))
		))
	(yes:
		(set:  Standards=HIPAA)
		(call: hipaaCompliance)
))
(end)

(>hipaaCompliance< ask:
	(text: HIPAA. Was the data received from a HIPAA covered entity or a business associate of one?)
	(yes:
		(ask:
			(text: Safe Harbor. do the data visually adhere to the HIPAA Safe Harbor Provision?)
			(terms:
				(Safe Harbor Provision: Dates in years, zip codes contain only first 2 digits))
			(yes: (ask: (text:Do you know of a way to to put names to the paitients in the data?)
					  	    (no:  (set: Code=green, Storage=clear, Transit=clear, auth=none, HIPAABasis=HIPAASafeHarbor, Effort=deidentified, Harm=minimal)
					  	    	    (end)))))
	)
)
(ask:
	(text: Statistician Provision. Has an expert certified the data as being of minimal risk?)
	(yes: (set:Code=green, Transit=clear, Storage=clear, auth=none, HIPAABasis=HIPAAStatistician, Effort=deidentified, Harm=minimal)
		    (end)))
(>3.1.3< ask:
	(text: Limited Data Set. Did you acquire the data under a HIPAA limited data use agreement?)
	(yes:
		(set: Storage=encrypt, Transit=encrypt, auth=approval, HIPAABasis=HIPAALimitedDataset, Effort=identifiable, Harm=criminal)
		(ask:
			(text: Did the limited data use agreement have any additional restrictions on sharing?)
			(yes:(call: hippa-dua)))
		(end)
	))
(>3.1.4< ask:
	(text: Business Associate. Did you acquire the data under a HIPAA Business Associate Agreement?)
	(yes:
		(set: Code=red, Storage=encrypt, Transit=encrypt, auth=approval, HIPAABasis=HIPAABusinessAssociate, Effort=identifiable, Harm=criminal)
		(ask: (text: Did the business associate agreement have any additional restrictions?)
				  (yes: (call: hippa-dua)))
		(end)
	))
(>3.1.5< ask:
	(text: Covered. Are you an entity that is directly or indirectly covered by HIPAA?)
	(yes:
		(set: Code=red, Storage=encrypt, Transit=encrypt, auth=approval, HIPAABasis=HIPAABusinessAssociate, Effort=identifiable, Harm=criminal)))
(end)

(>hippa-dua< todo)(end)


(>ppraCompliance< ask: 
	(text: Were the data collected by an elementary or secondary school and in connection with a program receiving funds from the US Department of Education?)
	(terms:
		(Elementary or secondary school: An elementary or secondary school is a public or private school serving students in any of the grades from kindergarten through high school.))
	(no: (end)))

(>ppra2< ask:
	(text: Do the data contain personal information from students concerning any of the following eight categories?
		      * Political affiliations or beliefs of a student or a student’s parent;
          * Mental and psychological problems of a student or a student’s family;
          * Sex behavior and attitudes;
          * Illegal, anti-social, self-incriminating, or demeaning behavior;
          * Critical appraisals of other individuals with whom respondents have close family relationships;
          * Legally recognized privileged or analogous relationships, such as those of lawyers, physicians, and ministers;
          * Religious practices, affiliations, or beliefs of a student or a student’s parent; or
          * Income, other than that required by law to determine eligibility for participation in a program or for receiving financial assistance under such program.)
 (terms:
	(personal information: individually identifiable information such as i. a student or parent’s first and last name; ii. a home or other physical address, including street name and the name of the city or town; iii. a telephone number; or iv. a Social Security identification number.
  ))
 (no: (end)))
(>ppra2a< ask:
	(text: Have the data been deidentified?)
	(yes: (set:Code=green, Harm=minimal, Storage=clear, Effort=deidentified) )
	(no:  (set:Code=orange, Harm=civil, Storage=encrypt, Effort=identifiable) ))
(>ppra3< ask:
	(text: Were the parents of the students whose information is contained in the data provided with annual Protection of Pupil Rights Amendment [PPRA] notices? These notices inform parents of their rights under the PPRA, of local education policies concerning collection of student information, and of the specific or approximate dates of the proposed data collection activity.)
	(no: (reject: Possible PPRA violation, due to failure to provide annual PPRA notices)))
(>ppra4< ask:
	(text: Were the data collected through a program, such as a survey, evaluation, or analysis, that was funded by the US Department of Education?)
	(yes:
		(>ppra5< ask:
			(text:Was written consent obtained from parents prior to the data collection?)
			(yes: (set: PPRATags=PPRAConsent) )
			(no: (reject: Possible PPRA violation, due to failure to obtain written consent for collection of information protected under the PPRA))
			))
	(no:
		(>ppra6< ask:
			(text: Were the data disclosed by the school for marketing or sale?)
			(no: (end)))
		(>ppra7< ask:
			(text: Were parents provided with notice and an opportunity to opt out of the collection and disclosure of the information?)
			(yes: (set:PPRATags=PPRAOptOutProvided) )
			(no: (reject: Possible PPRA violation due to failure to provide parents with the opportunity to opt out of the data collection.))
		)
	))
(set: Standards=PPRA)
(end)

(>ferpaCompliance< ask:
	(text:Do the data directly relate to a student, and were the data maintained by an educational agency or institution receiving funds from the US Department of Education? )
	(terms:
		(Educational agency or institution: A public or private agency or institution that provides educational services or instruction, or both, to students. This includes, but is not limited to, a primary or secondary school, college or university, school district, or state educational agency.)
		(Funds from the us department of education: This means funds provided by the US Department of Education to the agency or institution by grant, cooperative agreement, contract, subgrant, or subcontract; or provided to students attending the agency or institution. The funds may be paid to the agency or institution by those students for educational purposes, such as under the Pell Grant Program and the Guaranteed Student Loan Program.)
	)
	(no: (end))
)
(>ferpa9< ask:
	(text:Do the data fall into any of the following categories?
		* Personal memory aids held in the sole possession of their creator, such as a teacher's notes;
		* Records maintained by the law enforcement division of an educational agency or institution;
		* Employment records for an educational agency or institution;
		* Records produced by a physician, psychiatrist, or other professional for treatment purposes; or
		* Grades on peer-graded papers before a teacher has recorded them.
	)
	(yes: (end))
)
(set: Standards=FERPA)
( >ferpa10< ask:
	(text: Do the data include information that, alone or in combination, is linked or linkable to a specific student that would allow a reasonable person to identify the student with reasonable certainty? This includes, but is not limited to, a student’s or student’s family name, student or student’s family’s address, a personal identifier like a Social Security number or student number, or other indirect identifier like date of birth or mother’s maiden name.)
	(no:
		(set: Storage=clear, Code=green, Harm=minimal, Effort=deidentified, Transit=clear, FERPABasis=FERPADeidentified)
		(end))
)
( >ferpa11< ask:
	(text: Did the educational agency or institution classify the data as directory information?)
	(terms:
		(Directory information: Directory information is information that is generally not considered harmful or an invasion of privacy if released and can be disclosed to outside organizations without a parent's prior written consent. Categories of information typically designated as directory information include: student's name; address; telephone listing; e-mail address; photograph; date and place of birth; major field of study; dates of attendance; grade level; participation in officially recognized activities and sports; weight and height of members of athletic teams; degrees, honors, and awards received; the most recent educational agency or institution attended; and student ID number, user ID, or other unique personal identifier used in electronic records.)
	)
	(yes:
		( >ferpa11a< ask: (text: Did the educational agency or institution give parents and students notice of the type of information they are designating as directory information and the opportunity to opt out of the disclosure of their directory information? )
		  (yes:
		  	(set: Effort=identifiable )
				( >ferpa11ai< ask:
					(text: Did any of the students in the data request to opt out of the disclosure of their directory information? )
					(yes: (set: Code=yellow, Storage=encrypt, Harm=shame,      Transit=encrypt, FERPABasis=FERPADirectoryOptOut))
					(no:  (set: Code=green,  Storage=clear,   Harm=minimal, Transit=clear,   FERPABasis=FERPADirectoryInfo)))
				(end))
)))

(set: Transit=encrypt, Storage=encrypt, Code=yellow, Harm=shame, Effort=identifiable, Transit=encrypt)

( >ferpa12< ask:
	(text: Did the parents, guardians, or students [if 18 or over] sign a consent form authorizing this disclosure of their data?)
	(yes:
		( >ferpa12a< ask: 
			(text: Did the consent form specify the records to be disclosed, the purpose of the disclosure, and to whom the records may be disclosed?)
			(yes: (set: FERPABasis=FERPAConsent)
						(>FERPA-Consent< todo )
						(end))
		))
)
( >ferpa13< ask: 
	(text: Were the data disclosed to a school official or contractor with a legitimate educational interest?)
	(terms:
		(School official:
				This includes professors; instructors; administrators; health staff; counselors; attorneys; clerical staff; trustees; members of committees and disciplinary boards; and contractors, consultants, volunteers or other parties to whom the school has outsourced institutional services or functions.)
		(Legitimate educational interest:
				A school official generally has a legitimate educational interest if the official needs to review an education record in order to fulfill his or her professional responsibility.))
	(yes: (set: FERPABasis=FERPAOfficial)
			  (end))
)
( >ferpa14< ask:
	(text: Are the data being disclosed for the purpose of conducting a study for or on behalf of the educational agency or institution in order to develop, validate, or administer predictive tests; administer student aid projects; or improve instruction?)
	(yes: (set: FERPABasis=FERPAStudies) )
	(no: 
		(reject: educational agency or institution is likely breaching its FERPA duties because it is disclosing, or a third party is re-disclosing, non-directory PII without parental consent where no obvious FERPA exception applies))
)
(>FERPA-DUA< todo )
(end)


(>govRecsCompliance< call: Gov1 )
(call: DPPA1)
(call: ESRA1)
(call: CIPSEA1)
(call: PA1)
(end)

(>Gov1< ask:
	(text: Were the data collected by a federal agency or a researcher under contract with or with funds from a federal agency?)
	(no: (end) )
	(yes: 
		( >Gov2< ask:
			(text: Are you an employee or an agent, such as a contractor, consultant, researcher, or employee of a private organization, operating under a contract, grant, or other agreement with the agency that collected the data?)
			(yes:
				(>Census1< ask:
					(text: Were the data collected by the US Census Bureau?)
					(yes:
						(>Census2< ask:
							(text: Were the data published by the Census Bureau?)
							(yes:
								(set: Harm=minimal, Code=green, Effort=deidentified, Transit=clear, Storage=clear, Census=CensusPublished))
							(no: 
								(>Census3< ask:
									(text: Has a Census Bureau Disclosure Review Board issued an approval memorandum granting formal permission for the data to be published?)
									(yes: 
										(set: Harm=minimal, Code=green, Effort=deidentified, Transit=clear, Storage=clear, Census=CensusPublished))
									(no: 
										(reject: Possible violation of Title 13 due to failure to obtain permission from the Census Bureau to publish the Census data.)
))))))))))(end)

(>DPPA1< ask:
	(text: Do the data contain personal information obtained from a state department of motor vehicles?)
	(terms:
		(Personal information: Information that identifies an individual, including, but not limited to, an individual’s photograph, Social Security number, driver identification number, name, address [not including the 5-digit ZIP code], telephone number, or medical or disability information. It does not include information on vehicular accidents, driving violations, or a driver’s status [including whether the driver’s license is valid, suspended, or revoked]))
	(yes: 
		(>DPPA2< ask:
			(text: Do the department of motor vehicles records contain an individual's photograph or image, Social Security number, or medical or disability information?)
			(yes: 
				(>DPPA2a< ask:
					(text: Has express consent been obtained from each person whose photograph, Social Security number, or medical or disability information appears in the data?)
					(yes: 
						(set: DPPA=highlyRestricted, Code=orange, Harm=civil, Effort=identifiable, Transit=encrypt, Storage=encrypt))
					(no:
						(reject: Possible violation of the DPPA due to disclosure of highly restricted personal information without the consent of the individuals in the data))))
			(no: 
				(set: Code=yellow, Harm=shame, Effort=identifiable, Transit=encrypt, Storage=encrypt)
				(>DPPA3< ask:
					(text: Is disclosure of the personal information required by the Driver’s Privacy Protection Act? Disclosure is required, among other reasons, in connection with matters of motor vehicle or driver safety and theft; motor vehicle emissions; motor vehicle product alterations, recalls, or advisories; and performance monitoring of motor vehicles and dealers by motor vehicle manufacturers.)
					(yes:
						(set: DPPA=required))
					(no:
						(>DPPA4< ask:
							(text: Is the disclosure of these records made in accordance with the driver’s license record disclosure law of the state from which the records were obtained?)
							(no: 
								(reject: Possible violation of state law on disclosure of drivers’ records)))
						(>DPPA5< ask:
							(text: Was consent obtained from the person about whom the information pertains by the State DMV in response to a request for an individual record? )
							(yes:
								(>DPPA5a< ask:
									(text: Was the obtained consent limited in any way, such as purpose, duration, parties that may receive the data?)
									(yes: 
										(set: DPPA=stateConsentLimited))
									(no:
										(set: DPPA=stateConsentBroad))))
							(no: 
								(>DPPA6< ask:
									(text: Was consent obtained from the individuals whose information is contained in the data by the requester of the records?)
									(yes:
										(>DPPA6a< ask:
											(text: Was the obtained consent limited in any way, such as purpose, duration, parties that may receive the data?)
											(yes:
												(set: DPPA=requesterConsentLimited))
											(no: 
												(set: DPPA=requesterConsentBroad))))
									(no: 
										(>DPPA7< ask:
											(text: Were the data obtained for use in research activities?)
											(yes: (set: DPPA=research))
											(no: 
												(>DPPA8< ask:
													(text: Was the personal information obtained under one of the other fourteen permissible uses for the disclosure of drivers’ records, including:
														* for use by any government agency, or any person acting on behalf of a government agency, in carrying out its functions;
														* for use in connection with matters of motor vehicle or driver safety and theft; motor vehicle emissions; motor vehicle product alterations, recalls, or advisories; performance monitoring of motor vehicles, motor vehicle parts and dealers; motor vehicle market research activities, including survey research; and removal of non-owner records from the original owner records of motor vehicle manufacturers; or
														* for bulk distribution for surveys, marketing or solicitations if the State has obtained the express consent of the person to whom such personal information pertains.)
													(yes:
														(set: DPPA=exception))
													(no:
														(reject: possible violation of the DPPA because the depositor has not cited one of the required or permissible uses for disclosure.)
										)))))
)))))))))(end)

(>ESRA1< ask:
	(text: Do the data contain information from an education research project that receives funding from the Institute of Education Sciences? Examples of funded projects are included 	on the following web sites:
		* National Center for Education Research [NCER] - http://ies.ed.gov/ncer/projects/
		* National Center for Education Evaluation and Regional Assistance [NCEE] - http://ies.ed.gov/ncee/projects/
		* What Works Clearinghouse [WWC] - http://ies.ed.gov/ncee/wwc/
		* Regional Educational Laboratory Program - http://ies.ed.gov/ncee/edlabs/index.asp
		* Education Resources Information Center [ERIC] - http://ies.ed.gov/ncee/projects/eric.asp
		* National Library of Education [NLE] - http://ies.ed.gov/ncee/projects/nle/
		* National Center for Special Education Research [NCSER] - http://ies.ed.gov/ncser/projects/
		* National Center for Education Statistics [NCES] - http://nces.ed.gov/surveys/)
	(yes: 
		(>ESRA2< ask:
			(text: Have the data been published previously, or did you submit a copy of the data to the Institute of Education Sciences [IES] Data Security Office and receive its authorization to disclose the data?)
			(no: (reject: Possible violation of ESRA due to failure to obtain authorization to disclose the data from the Institute of Education Sciences))
			(yes: 
				(>ESRA3< ask:
					(text: Has the IES Data Security Office limited access to the data to individuals authorized by an IES license agreement or individuals who have executed an affidavit of nondisclosure?)
					(yes: (set: Code=yellow, Harm=shame, Transit=encrypt, Storage=encrypt, ESRA=restricted ) )
					(no:  (set: Code=green, Harm=minimal, Transit=clear, Storage=clear, ESRA=public ) )
)))))(end)

(>CIPSEA1< ask:
	(text: Were the data collected directly from respondents for exclusively statistical purposes under a pledge of confidentiality?)
	(yes: 
		(>CIPSEA2< ask:
			(text: Is the agency that collected the data a statistical agency or unit from this list: USDA Economic Research Service, National Agricultural Statistics Service, Bureau of Economic Analysis, Census Bureau, National Center for Education Statistics, Energy Information Administration, National Center for Health Statistics, Bureau of Justice Statistics, Bureau of Labor Statistics, Bureau of Transportation Statistics, Statistics of Income Division of the Internal Revenue Service, or NSF Division of Science Resources Statistics?)
			(yes: 
				(>CIPSEA3< ask:
					(text: Are the data being shared in identifiable form?)
					(terms:
						(Identifiable form:
						Identifiable form refers to any representation of information that permits the identity of the respondent to whom the information applies to be reasonably inferred by either direct or indirect means.)
					(no:  (set: CIPSEA=deidentified) )
					(yes: (set: CIPSEA=identifiable) (todo: CIPSEA DUA) )
)))))(end)

(>PA1< ask:
	(text:Are the data maintained by the agency in a system of records?)
	(terms:
		(System of records:
		A system of records is a group of any records under the control of any agency from which information is retrieved by the name of the individual or by some identifying number, symbol, or other identifying particular assigned to the individual.)
		(Record:
		A record is any item, collection, or grouping of information about an individual that is maintained by an agency, including, but not limited to, his education, financial transactions, medical history, and criminal or employment history, and that contains his name, or the identifying number, symbol, or other identifying particular assigned to the individual, such as a finger or voice print or a photograph.))
	(yes:
		(>PA2< ask:
			(text: Do the data contain personally identifiable information?)
			(terms:
				(Personally identifiable information:
				Any information about an individual that can be used to distinguish or trace an individual‘s identity - such as name, address, or Social Security number - either by itself or in combination with any other information that is linked or linkable to an individual, such as medical, educational, financial, criminal or employment history.))
			(yes: (set: PrivacyAct=identifiable) (todo: Privacy Act DUA) )
			(no:  (set: PrivacyAct=deidentified) )
)))(end)

(>dua< todo: Data use agreements)
( >timeLimit< ask:
	(text: Is there any reason why we cannot store the data indefinitely?
	       Limiting the time a dataset could be held interferes with good science practices such as replication, and should thus be avoided whenever possible )
	(no: (set: TimeLimit=none) )
	(yes:        
		(ask:
			(text:For how long should we keep the data?)
			(indefinitely:  (set: TimeLimit=none) )
			(50 years: (set: TimeLimit=_50yr) )
			(5 years:  (set: TimeLimit=_5yr) )
			(1 year:   (set: TimeLimit=_1yr) )
		)
	)
)
(end)
